#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["laser attack"]
#Text["."]
#System["./../system/rb_System.dnh"]
#Background["./../system/stage_bg/Background_Cirno0.dnh"]
#include "./../system/rb_Game_index.dnh"
#include "./boss_daiyousei.dnh"

let attack_duration = 160;
let attack_life = 10000;

@Initialize
{
	objPlayer = GetPlayerObjectID();
	LoadSE;
	objEnemy = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objEnemy);
	TInit;
}
@Event
{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)
	{
		SetScriptResult(attack_life);
	}
	case(EV_REQUEST_TIMER)
	{
		SetScriptResult(attack_duration);
	}
}
@MainLoop
{
	yield;
}

task TInit{
	BossAnimInit;
	let cx = GetStgFrameWidth()/2;
	TBossMoveTowardsAnim(objEnemy, cx+rand(-32, 32), 60+rand(-4, 8)+56, 45, 0,0.2);
	BombHold;
	StartAttack("Test \"Custom Lasers\"", ATTACK_SPELLCARD, END_BASIC, 0.5, 4, 1, attack_duration, 1500);
	BossCollision(60, 16, 8, 0);
	BombShield(SHIELD_BOMBS, 200, 100, 100, 100, 100, 0);
	TAttack;
}

//Prevent a bullet from being deleted when it goes offscreen, for a limited time.
task TOffscreenBullet(obj, time){
	ObjShot_SetAutoDelete(obj,false);
	loop(time){
		//if(GetPlayerState==STATE_DOWN||IsPlayerSpellActive){break;}
		yield;
	}
	if(!Obj_IsDeleted(obj)){ObjShot_SetAutoDelete(obj,true);}
}

task TAttack
{
	//wait(180);

	wait(60);
	wait(30);

	let ex = ObjMove_GetX(objEnemy);
	let ey = ObjMove_GetY(objEnemy)+shoot_offset;
	let px = GetPlayerX;

	//let all_dir = (truncate(rand(0,2))-0.5)*2;
	let all_dir = -1;

	//Set2DCameraRatio(0.75);
	//TSetFPS(210, 5);
	//SetPlayerInvincibilityFrame(99999);
	while(!finish) {
		let cx = GetStgFrameWidth()/2;

		TPhase3(all_dir);
		waitb(120);
		TPhase3(all_dir);
		waitb(120);
		TPhase1(all_dir);
		waitb(120);
		TPhase1(-all_dir);
		waitb(120);
		TPhase4(all_dir);
		waitb(120);
		TPhase2(all_dir);
		waitb(120);

		all_dir = -all_dir;
	}

	task TSetFPS(delay, rate){
		loop(delay){yield;}
		if(!finish){
			StartSlow(TARGET_ALL,rate);
		}
	}
	task TResetFPS(delay){
		loop(delay){yield;}
		if(!finish){
			StopSlow(TARGET_ALL);
		}
	}
	function TPhase3(dir){
		SetShotAutoDeleteClip(16,64,16,0);//This suddenly becomes important for performance
		TBossMoveTowardsAnim(objEnemy, bossSTGw/2+rand(-32, 32), 60+rand(-8, 16)+56, 60, 0,0.2);
		wait(30);
		if(!finish){
			let ex = ObjMove_GetX(objEnemy);
			let ey = ObjMove_GetY(objEnemy)+shoot_offset;
			BulletFlash(ex, ey, 0.15, rand(0,360), AS_FLASH_BLUE, 30,30, 6,6, 0.5,0.5, 0); // A warning flash that bullets are spawning
			objEnemyAnim = BossAnim1(objEnemy, objEnemyAnim, 6, 360, 0.25, 0, 0.125);
		}
		wait(30);
		if(!finish){
			//TBossMoveTowardsAnim(objEnemy, bossSTGl+bossSTGw/3+rand(-32, 32), 60+rand(-8, 16)+56, 45, 0,0.2);
			ex = ObjMove_GetX(objEnemy);
			ey = ObjMove_GetY(objEnemy)+shoot_offset;
			//Staff(0, 1, AS_LASER_DORANGE);
			//Staff(30, -1, AS_LASER_DPURPLE);
			StaffLow(0, 1, AS_BALL_S_DORANGE);
			StaffLow(20, -1, AS_BALL_S_DPURPLE);
		}
		wait(60);
		if(!finish){
			Stuff(AS_BALL_BS_BLUE,AS_STAR_S_PURPLE,AS_RICE_S_RED);
		}
		wait(90);
		if(!finish){
			//Staff(0, 1, AS_LASER_DORANGE);
			//Staff(30, -1, AS_LASER_DPURPLE);
			StaffLow(0, 1, AS_BALL_S_DORANGE);
			StaffLow(40, -1, AS_BALL_S_DPURPLE);
		}
		wait(60);

		task Stuff(bullet1,bullet2,bullet3){
			let ang = 0;
			let obj;
			let rn = 0;
			let dir=1;
			let rof=16;
			loop(3){
				let ex = ObjMove_GetX(objEnemy);
				let ey = ObjMove_GetY(objEnemy)+shoot_offset;
				ang=atan2(GetPlayerY-ey,GetPlayerX-ex);
				loop(2){
					spellbreak;
					let bx=ex+rand(-rn,rn);
					let by=ey+rand(-rn,rn);
					ascent(i in 0..4){
						spellbreak;
						PlaySnd(SND_e_shot, 100);
						BulletFlash(bx,by, 0.5, rand(0,360), AS_FLASH_BLUE, 10,30, 2,2, 0.5,0.5, 0); // A warning flash that bullets are spawning
						ascent(j in 0..12){
							let bullet=bullet1;
							if(i%2!=0){
								bullet=bullet2;
							}
							obj = CustomShotA2(bx,by, 4,ang+(i*3)*dir,-2/15,1+i*0.2, bullet, 20, 0);
							//ObjMove_AddPatternA2(obj, 50-i*2, NO_CHANGE, ObjMove_GetAngle(obj)+(45+i*5)*dir, 0, 0, 10);
							ObjMove_AddPatternA2(obj, 50, NO_CHANGE, NO_CHANGE, 0, (30)/10*dir, 10);
							ObjMove_AddPatternA2(obj, 60, NO_CHANGE, NO_CHANGE, 0, 0, 10);
							if(i>-1){
								obj = CustomShotA2(bx,by, 4,ang-(i*3-1)*dir,-2/15,1+i*0.2, bullet, 20, 0);
								//ObjMove_AddPatternA2(obj, 50-i*2, NO_CHANGE, ObjMove_GetAngle(obj)+(45+i*5)*dir, 0, 0, 10);
								ObjMove_AddPatternA2(obj, 50, NO_CHANGE, NO_CHANGE, 0, -(30)/10*dir, 10);
								ObjMove_AddPatternA2(obj, 60, NO_CHANGE, NO_CHANGE, 0, 0, 10);
							}
							ang+=360/12;
						}
						wait(3);
					}
					ascent(j in 0..0){
						bx=ex+rand(-24,24);
						by=ey+rand(-24,24);
						BulletFlash(bx,by, 0.5, rand(0,360), AS_FLASH_RED, 10,30, 3,3, 0.5,0.5, 0); // A warning flash that bullets are spawning
						obj = CustomShotA2(bx,by, 2,ang+rand(-2,2),3/30,5, bullet3, 20, 0);
						ang+=360/24;
					}
					wait(rof);
					//rof-=2;
					rn+=1;
					ang+=180/16;
				}
				dir=-dir;
			}
		}
		task StaffHigh(delay, ang, dir, bullet){
			wait(delay);
			if(!finish){
				PlaySnd(SND_e_shot, 100);
				ascent(i in 1..8){
					PlaySndDelay(i*2, SND_e_shot, 100);
				}
				PlaySndDelay(95, SND_e_shot_re, 100);
				let ex = ObjMove_GetX(objEnemy);
				let ey = ObjMove_GetY(objEnemy)+shoot_offset;
				//BulletFlash(ex, ey, 0.5, ang, AS_FLASH_YELLOW, 10,30, 3,3, 0.5,0.5, 0); // A warning flash that bullets are spawning
				ascent(i in 0..5){
					if(finish){break;}
					let obj;
					//obj = CreateCurveLaserA1(ex,ey, 0,ang-i*5*dir, 100,8, bullet, 20);
					//ObjMove_AddPatternA2(obj, 20, 6, NO_CHANGE, -5/70, 2*dir, 1);
					obj = MyLaserz(ex,ey, 9,ang+i*10*dir, -7/40,0.5*dir,1, 20, 4, bullet, 20);
					TOffscreenBullet(obj,150);
					ObjMove_AddPatternA2(obj, 60, NO_CHANGE, NO_CHANGE, 7/30, (20.4-i^1.05*1.5)*dir, 8);
					ObjMove_AddPatternA2(obj, 70, NO_CHANGE, NO_CHANGE, 7/30, -0.67*dir, 8);
					//ObjMove_AddPatternA2(obj, 95, NO_CHANGE, NO_CHANGE, 8/75, 1*dir, 8);
					//ObjMove_AddPatternA2(obj, 120, NO_CHANGE, NO_CHANGE, 8/75, 0.5*dir, 8);
					wait(2);
				}
			}
		}
		task StaffMed(delay, dir, bullet){
			wait(delay);
			if(!finish){
				PlaySnd(SND_e_shot, 100);
				ascent(i in 1..8){
					PlaySndDelay(i*2, SND_e_shot, 100);
				}
				PlaySndDelay(95, SND_e_shot_re, 100);
				let ex = ObjMove_GetX(objEnemy);
				let ey = ObjMove_GetY(objEnemy)+shoot_offset;
				let ang=atan2(GetPlayerY-ey,GetPlayerX-ex)+180+30*dir;
				//BulletFlash(ex, ey, 0.5, ang, AS_FLASH_YELLOW, 10,30, 3,3, 0.5,0.5, 0); // A warning flash that bullets are spawning
				ascent(i in 0..5){
					if(finish){break;}
					let obj;
					//obj = CreateCurveLaserA1(ex,ey, 0,ang-i*5*dir, 100,8, bullet, 20);
					//ObjMove_AddPatternA2(obj, 20, 6, NO_CHANGE, -5/70, 2*dir, 1);
					obj = MyLaserz(ex,ey, 6,ang-i*5*dir, -5/70,2*dir,1, 20, 4, bullet, 20);
					TOffscreenBullet(obj,150);
					ObjMove_AddPatternA2(obj, 80, NO_CHANGE, NO_CHANGE, 8/75, (2.9+i^0.95*1.8)*dir, 8);
					ObjMove_AddPatternA2(obj, 90, NO_CHANGE, NO_CHANGE, 8/75, 0.5*dir, 8);
				}
			}
		}
		task StaffLow(delay, dir, bullet){
			wait(delay);
			if(!finish){
				PlaySnd(SND_e_shot, 100);
				ascent(i in 1..8){
					PlaySndDelay(i*2, SND_e_shot, 100);
				}
				PlaySndDelay(95, SND_e_shot_re, 100);
				let ex = ObjMove_GetX(objEnemy);
				let ey = ObjMove_GetY(objEnemy)+shoot_offset;
				let ang=atan2(GetPlayerY-ey,GetPlayerX-ex)+180+105*dir;
				BulletFlash(ex, ey, 0.15, ang-50*dir, AS_FLASH_ORANGE, 10,120, 2,2, 0.5,0.5, 0); // A warning flash that bullets are spawning
				//BulletFlash(ex, ey, 0.5, ang, AS_FLASH_YELLOW, 10,30, 3,3, 0.5,0.5, 0); // A warning flash that bullets are spawning
				ascent(i in 0..5){
					if(finish){break;}
					let obj;
					//obj = CreateCurveLaserA1(ex,ey, 0,ang-i*22*dir, 100,8, bullet, 20);
					//ObjMove_AddPatternA2(obj, 20, 7.5, NO_CHANGE, -6.5/60, (-0.2+i*0.4)*dir, 1);
					obj = MyLaserz(ex,ey, 7.5,ang-i*22*dir, -6.5/60,(-0.2+i*0.4)*dir,1, 20, 4, bullet, 20);
					TOffscreenBullet(obj,150);
					ObjMove_AddPatternA2(obj, 80, NO_CHANGE, NO_CHANGE, 6/60, (3.6+i*0.05)*dir, 7);
					ObjMove_AddPatternA2(obj, 110, NO_CHANGE, NO_CHANGE, 6/60, 0.67*dir, 7);
					//ObjMove_AddPatternA2(obj, 110, NO_CHANGE, NO_CHANGE, 2.5/40, 0.67*dir, 6.5);
					//ObjMove_AddPatternA2(obj, 125, NO_CHANGE, NO_CHANGE, 2.5/40, 0.33*dir, 6.5);
					//ObjMove_AddPatternA2(obj, 95, NO_CHANGE, NO_CHANGE, 8/75, 1*dir, 8);
					//ObjMove_AddPatternA2(obj, 120, NO_CHANGE, NO_CHANGE, 8/75, 0.5*dir, 8);
					wait(2);
				}
			}
		}
	}
	function TPhase1(dir)
	{
		SetShotAutoDeleteClip(16,64,16,16);//This suddenly becomes important for performance
		let ang = rand(0,360);
		TBossMoveTowardsAnim(objEnemy, bossSTGw/2+rand(-32, 32), 60+rand(-8, 16)+56, 60, 0,0.2);
		wait(30);
		if(!finish){
			let ex = ObjMove_GetX(objEnemy);
			let ey = ObjMove_GetY(objEnemy)+shoot_offset;
			BulletFlash(ex, ey, 0.15, rand(0,360), AS_FLASH_BLUE, 30,30, 6,6, 0.5,0.5, 0); // A warning flash that bullets are spawning
			objEnemyAnim = BossAnim1(objEnemy, objEnemyAnim, 6, 240, 0.25, 0, 0.125);
		}
		wait(30);
		if(!finish){
			//TBossMoveTowardsAnim(objEnemy, bossSTGl+bossSTGw/3+rand(-32, 32), 60+rand(-8, 16)+56, 45, 0,0.2);
			ex = ObjMove_GetX(objEnemy);
			ey = ObjMove_GetY(objEnemy)+shoot_offset;
			PlaySnd(SND_e_shot, 100);
			ascent(i in 1..8){
				PlaySndDelay(i*3, SND_e_shot, 100);
			}
			PlaySndDelay(95, SND_e_shot_re, 100);
			ascent(i in 0..16){
				let obj;
				if(dir>0){
					if(i%2==0){
						BulletFlashAdd(ex, ey, 0.5, ang+i*360/16+rand(5,15), AS_FLASH_YELLOW, 10,30, 4,4, 0.5,0.5, 0); // A warning flash that bullets are spawning
						BulletFlashAdd(ex, ey, 1, ang+i*360/16+rand(5,15), AS_FLASH_BLUE, 10,30, 2,2, 0.5,0.5, 0); // A warning flash that bullets are spawning
					}else{
						BulletFlash(ex, ey, 0.5, ang+i*360/16+rand(5,15), AS_FLASH_YELLOW, 10,30, 4,4, 0.5,0.5, 0); // A warning flash that bullets are spawning
						BulletFlash(ex, ey, 1, ang+i*360/16+rand(5,15), AS_FLASH_BLUE, 10,30, 2,2, 0.5,0.5, 0); // A warning flash that bullets are spawning
					}
					//This is a pretty ugly way to create these bullets
					obj = CustomShotA2(ex,ey, 3.5,ang+i*360/16,0,10, AS_RICE_M_YELLOW, 20, 0);
					obj = CustomShotA2(ex,ey, 3.5,ang+i*360/16+5,0,10, AS_RICE_M_YELLOW, 25, 0);
					obj = CustomShotA2(ex,ey, 3.5,ang+i*360/16+10,0,10, AS_RICE_M_YELLOW, 30, 0);
					obj = CustomShotA2(ex,ey, 3.5,ang+i*360/16+15,0,10, AS_RICE_M_YELLOW, 35, 0);
					obj = CustomShotA2(ex,ey, 3.5,ang+i*360/16+20,0,10, AS_RICE_M_YELLOW, 40, 0);
					obj = CustomShotA2(ex,ey, 2.5,ang+i*360/16,0,10, AS_KNIFE_BLUE, 20, 0);
					obj = CustomShotA2(ex,ey, 2.5,ang+i*360/16-5,0,10, AS_KNIFE_BLUE, 25, 0);
					obj = CustomShotA2(ex,ey, 2.5,ang+i*360/16-10,0,10, AS_KNIFE_BLUE, 30, 0);
					obj = CustomShotA2(ex,ey, 2.5,ang+i*360/16-15,0,10, AS_KNIFE_BLUE, 35, 0);
					obj = CustomShotA2(ex,ey, 2.5,ang+i*360/16-20,0,10, AS_KNIFE_BLUE, 40, 0);
				}
				else if(dir<0){
					let rx=rand(-32,32);
					let ry=rand(-32,32);
					if(i%2==0){
						BulletFlash(ex+rx, ey+ry, 0.5, ang+i*360/16+rand(5,15), AS_FLASH_BLUE, 10,30, 4,4, 0.5,0.5, 0); // A warning flash that bullets are spawning
						BulletFlash(ex+rx, ey+ry, 1, ang+i*360/16+rand(5,15), AS_FLASH_SKY, 10,30, 2,2, 0.5,0.5, 0); // A warning flash that bullets are spawning
					}else{
						BulletFlashAdd(ex+rx, ey+ry, 0.5, ang+i*360/16+rand(5,15), AS_FLASH_BLUE, 10,30, 4,4, 0.5,0.5, 0); // A warning flash that bullets are spawning
						BulletFlashAdd(ex+rx, ey+ry, 1, ang+i*360/16+rand(5,15), AS_FLASH_SKY, 10,30, 2,2, 0.5,0.5, 0); // A warning flash that bullets are spawning
					}
					//This is a pretty ugly way to create these bullets
					obj = CustomShotA2(ex+rx,ey+ry, 2.5+rand(-0.5,0.5),ang+i*360/16+rand(-5,5),0,10, AS_RICE_M_A_SKY, 0, 0);
					obj = CustomShotA2(ex+rx,ey+ry, 2.5+rand(-0.5,0.5),ang+i*360/16-5+rand(-5,5),0,10, AS_RICE_M_A_SKY, 5, 0);
					obj = CustomShotA2(ex+rx,ey+ry, 2.5+rand(-0.5,0.5),ang+i*360/16-10+rand(-5,5),0,10, AS_RICE_M_A_SKY, 10, 0);
					obj = CustomShotA2(ex+rx,ey+ry, 2.5+rand(-0.5,0.5),ang+i*360/16-15+rand(-5,5),0,10, AS_RICE_M_A_SKY, 15, 0);
					obj = CustomShotA2(ex+rx,ey+ry, 2.5+rand(-0.5,0.5),ang+i*360/16-20+rand(-5,5),0,10, AS_RICE_M_A_SKY, 20, 0);
					obj = CustomShotA2(ex+rx,ey+ry, 3.5+rand(-0.5,0.5),ang+i*360/16,0,10, AS_RICE_S_BLUE, 0, 0);
					obj = CustomShotA2(ex+rx,ey+ry, 3.5+rand(-0.5,0.5),ang+i*360/16+5+rand(-5,5),0,10, AS_RICE_S_BLUE, 5, 0);
					obj = CustomShotA2(ex+rx,ey+ry, 3.5+rand(-0.5,0.5),ang+i*360/16+10+rand(-5,5),0,10, AS_RICE_S_BLUE, 10, 0);
					obj = CustomShotA2(ex+rx,ey+ry, 3.5+rand(-0.5,0.5),ang+i*360/16+15+rand(-5,5),0,10, AS_RICE_S_BLUE, 15, 0);
					obj = CustomShotA2(ex+rx,ey+ry, 3.5+rand(-0.5,0.5),ang+i*360/16+20+rand(-5,5),0,10, AS_RICE_S_BLUE, 20, 0);
				}
				//obj = CreateCurveLaserA1(ex,ey, 0,ang+i*360/16, 32,8, AS_LASER_DSKY, 20);
				//ObjMove_AddPatternA2(obj, 20, 3, NO_CHANGE, 0, 3, 8);
				obj = MyLaserz(ex,ey, 4,ang+i*360/16,0,0,10, 10, 3, AS_BALL_S_DSKY, 20);
				TOffscreenBullet(obj,90);

				ObjMove_AddPatternA2(obj, 30, NO_CHANGE, NO_CHANGE, 0, 4, 8);
				ObjMove_AddPatternA2(obj, 80, NO_CHANGE, NO_CHANGE, 4/40, 1.5, 8);
				if(dir>0){
					ObjMove_AddPatternA2(obj, 110, NO_CHANGE, NO_CHANGE, 4/40, 1.5, 8);
				}else{
					ObjMove_AddPatternA2(obj, 110, 6, NO_CHANGE, 0, -0.75, 6);
					ObjMove_AddPatternA2(obj, 150, 6, NO_CHANGE, 0, -0.25, 6);
				}
				if(true){
					//obj = CreateCurveLaserA1(ex,ey, 0,ang+i*360/16, 64,8, AS_LASER_DRED, 20);
					//ObjMove_AddPatternA2(obj, 20, 3, NO_CHANGE, 0, -4, 8);
					obj = MyLaserz(ex,ey, 3,ang+i*360/16,0,-4,10, 20, 3, AS_BALL_S_DRED, 20);
					TOffscreenBullet(obj,90);

					//ObjMove_SetAngularVelocity(obj, -4);
					//ObjMove_AddPatternA2(obj, 20, 3, NO_CHANGE, 0, -4, 8);
					ObjMove_AddPatternA2(obj, 80, NO_CHANGE, NO_CHANGE, 5/40, -3, 8);
					if(dir>0){
						ObjMove_AddPatternA2(obj, 110, NO_CHANGE, NO_CHANGE, 4/40, -1.5, 8);
					}else{
						ObjMove_AddPatternA2(obj, 110, 6.5, NO_CHANGE, 0, 0.5, 6.5);
					}
				}

			}
		}
	}
	function TPhase2(dir)
	{
		SetShotAutoDeleteClip(16,64,16,16);//This suddenly becomes important for performance
		let ang = rand(0,360);
		TBossMoveTowardsAnim(objEnemy, bossSTGw/2+rand(-32, 32), 60+rand(-8, 16)+56, 60, 0,0.2);
		wait(30);
		if(!finish){
			let ex = ObjMove_GetX(objEnemy);
			let ey = ObjMove_GetY(objEnemy)+shoot_offset;
			BulletFlash(ex, ey, 0.15, rand(0,360), AS_FLASH_BLUE, 30,30, 6,6, 0.5,0.5, 0); // A warning flash that bullets are spawning
			objEnemyAnim = BossAnim1(objEnemy, objEnemyAnim, 6, 240, 0.25, 0, 0.125);
		}
		wait(30);
		if(!finish){
			//TBossMoveTowardsAnim(objEnemy, bossSTGl+bossSTGw/3+rand(-32, 32), 60+rand(-8, 16)+56, 45, 0,0.2);
			ex = ObjMove_GetX(objEnemy);
			ey = ObjMove_GetY(objEnemy)+shoot_offset;
			Opposing;
		}
		ascent(i in 0..90){
			spellbreak;
			PlaySnd(SND_e_shot_re, 100);
			if(i%8==0){
				BulletFlash(ex, ey, 0.15, ang, AS_FLASH_YELLOW, 20,30, 3,3, 0.5,0.5, 0); // A warning flash that bullets are spawning
			}
			let obj;
			//obj = CreateCurveLaserA1(ex,ey, 0,ang, 32,8, AS_BALL_S_DYELLOW, 20);
			//ObjMove_AddPatternA2(obj, 20, 4, NO_CHANGE, 6/60, 4, 6);
			obj = MyLaserz(ex,ey, 4,ang,6/60,4,6, 6, 3, AS_BALL_S_DYELLOW, 20);
			//ObjMove_SetAngularVelocity(obj, 4);
			//ObjMove_AddPatternA2(obj, 20, 4, NO_CHANGE, 6/60, 4, 10);
			ObjMove_AddPatternA2(obj, 40, NO_CHANGE, NO_CHANGE, 6/60, 2, 6);
			ObjMove_AddPatternA2(obj, 60, NO_CHANGE, NO_CHANGE, 6/60, 1, 6);
			ObjMove_AddPatternA2(obj, 90, NO_CHANGE, NO_CHANGE, 6/60, 0, 6);
			ang+=43;
			wait(2);
		}
		wait(30);

		ang = atan2(GetPlayerY-ey,GetPlayerX-ex)+90;
		ascent(i in 0..32){
			spellbreak;
			PlaySnd(SND_e_shot_re, 100);

			if(i%8==0){
				BulletFlash(ex, ey, 0.15, ang, AS_FLASH_YELLOW, 20,30, 2,2, 0.5,0.5, 0); // A warning flash that bullets are spawning
			}
			let obj;
			ascent(j in 0..2){
				//obj = CreateCurveLaserA1(ex,ey, 0,ang, 32,8, AS_BALL_S_DYELLOW, 20);
				//ObjMove_AddPatternA2(obj, 20, 3, NO_CHANGE, 4/60, 4, 10);
				obj = MyLaserz(ex,ey, 3,ang,4/60,4,10, 3, 4, AS_BALL_S_DYELLOW, 20);
				//ObjMove_SetAngularVelocity(obj, 4);
				//ObjMove_AddPatternA2(obj, 20, 3, NO_CHANGE, 4/60, 3, 10);
				ObjMove_AddPatternA2(obj, 60, NO_CHANGE, NO_CHANGE, 4/60, 0, 10);
				ang+=180;
			}
			ang+=7.5;
			wait(1);
		}
		//ang+=90;
		ascent(i in 0..48){
			spellbreak;
			PlaySnd(SND_e_shot_re, 100);
			if(i%4==0){
				BulletFlash(ex, ey, 0.5, ang, AS_FLASH_YELLOW, 20,30, 2,2, 0.5,0.5, 0); // A warning flash that bullets are spawning
			}
			let obj;
			ascent(j in 0..2){
				//obj = CreateCurveLaserA1(ex,ey, 0,ang, 32,8, AS_BALL_S_ORANGE, 20);
				//ObjMove_AddPatternA2(obj, 20, 2, NO_CHANGE, 3/60, 4, 10);
				obj = MyLaserz(ex,ey, 2,ang,3/60,4,10, 3, 4, AS_BALL_S_ORANGE, 20);
				TOffscreenBullet(obj,90);
				//ObjMove_SetAngularVelocity(obj, 4);
				//ObjMove_AddPatternA2(obj, 20, 2, NO_CHANGE, 3/60, 2, 10);
				if(i%2==0){
					ObjMove_AddPatternA2(obj, 60, NO_CHANGE, NO_CHANGE, 3/60, 0, 10);
				}
				else{
					ObjMove_AddPatternA2(obj, 60, NO_CHANGE, NO_CHANGE, -3/30, -6, 2);
					ObjMove_AddPatternA2(obj, 90, NO_CHANGE, NO_CHANGE, 3/40, 0, 10);
				}
				ang+=180;
			}
			ang+=-5;
			wait(2);
		}

		task Opposing{
			wait(48);
			let ang = rand(0,360);
			ascent(i in 0..10){
				spellbreak;
				PlaySnd(SND_e_shot_re, 100);
				BulletFlash(ex, ey, 0.15, ang, AS_FLASH_YELLOW, 20,30, 3,3, 0.5,0.5, 0); // A warning flash that bullets are spawning
				ascent(i in 0..9){
					let obj;
					//obj = CreateCurveLaserA1(ex,ey, 0,ang, 32,8, AS_BALL_S_ORANGE, 20);
					//ObjMove_AddPatternA2(obj, 20, 4, NO_CHANGE, 6/60, -4, 6);
					obj = MyLaserz(ex,ey, 4,ang,6/60,-4,6, 6, 3, AS_BALL_S_ORANGE, 20);
					//ObjMove_SetAngularVelocity(obj, 4);
					//ObjMove_AddPatternA2(obj, 20, 4, NO_CHANGE, 6/60, -4, 6);
					ObjMove_AddPatternA2(obj, 40, NO_CHANGE, NO_CHANGE, 6/60, -2, 6);
					ObjMove_AddPatternA2(obj, 60, NO_CHANGE, NO_CHANGE, 6/60, -1, 6);
					ObjMove_AddPatternA2(obj, 90, NO_CHANGE, NO_CHANGE, 6/60, 0, 6);
					ang+=360/9;
				}
				ang+=-27;
				wait(15);
			}
		}
	}
	function TPhase4(dir)
	{
		SetShotAutoDeleteClip(16,64,16,0);//This suddenly becomes important for performance
		let ang=rand(0,360);
		loop(2){
			TBossMoveTowardsAnim(objEnemy, bossSTGw/2+rand(-32, 32), 60+rand(-8, 16)+56, 60, 0,0.2);
			wait(30);
			spellbreak;
			let ex = ObjMove_GetX(objEnemy);
			let ey = ObjMove_GetY(objEnemy)+shoot_offset;
			BulletFlash(ex, ey, 0.15, rand(0,360), AS_FLASH_BLUE, 30,30, 6,6, 0.5,0.5, 0); // A warning flash that bullets are spawning
			objEnemyAnim = BossAnim1(objEnemy, objEnemyAnim, 6, 240, 0.25, 0, 0.125);
			wait(30);
			spellbreak;
			//TBossMoveTowardsAnim(objEnemy, bossSTGl+bossSTGw/3+rand(-32, 32), 60+rand(-8, 16)+56, 45, 0,0.2);
			ex = ObjMove_GetX(objEnemy);
			ey = ObjMove_GetY(objEnemy)+shoot_offset;
			BulletFlash(ex, ey, 0.15, rand(0,360), AS_FLASH_YELLOW, 10,30, 3,3, 0.5,0.5, 0); // A warning flash that bullets are spawning
			PlaySnd(SND_e_shot, 100);
			ascent(i in 1..8){
				PlaySndDelay(i*3, SND_e_shot, 100);
			}
			PlaySndDelay(115, SND_e_shot_re, 100);
			if(!finish){
				let bullet = [AS_BALL_S_DRED, AS_BALL_S_DGREEN, AS_BALL_S_DORANGE];
				ascent(i in 0..10){
					ascent(i in 0..3){
						let obj = MyLaserz(ex,ey, 1+2*i,ang,0,(1+1.6*i)*dir,10, 5, 2, bullet[i%3], 20);
						ObjMove_AddPatternA2(obj, 80, NO_CHANGE, NO_CHANGE, -3/30, -1*dir, 1.5);
						//ObjMove_AddPatternA2(obj, 110, NO_CHANGE, NO_CHANGE, 5/(30+i*10), 0, 6);
						ReplaceLaser(obj,110, 5/(30+i*10), 0, 6, 4,6);
						obj = MyLaserz(ex,ey, 1+2*i,ang,0,(-1-1.6*i)*dir,10, 5, 2, bullet[i%3], 20);
						ObjMove_AddPatternA2(obj, 80, NO_CHANGE, NO_CHANGE, -3/30, 1*dir, 1.5);
						//ObjMove_AddPatternA2(obj, 110, NO_CHANGE, NO_CHANGE, 5/(30+i*10), 0, 6);
						ReplaceLaser(obj,110, 5/(30+i*10), 0, 6, 4,6);
					}
					ang+=360/10;
				}
				//TSetFPS(145, 30);
				//TResetFPS(180);
				wait(30);
				ascent(j in 0..48){
					let obj=CustomShotA2(ex+rand(-2,2),ey+rand(-2,2), 3.0,ang,2/30,5.0, AS_SCALE_PURPLE, 20, 0);
					ObjRender_SetScaleY(obj,1.5);
					obj=CustomShotA2(ex+rand(-2,2),ey+rand(-2,2), 4,ang,-2/30,1.5, AS_RICE_S_RED, 20, 0);
					obj=CustomShotA2(ex+rand(-2,2),ey+rand(-2,2), 4,ang+360/128,-2/50,1.5, AS_RICE_S_RED, 20, 0);
					ang+=360/48;
				}
			}
			ang+=360/20;
			dir=-dir;
		}
	}
	task ReplaceLaser(obj,time, accel,av,maxspd, segments,seg_rate){//During the laser's final movement, replace it with a cheaper, less segmented one.
		wait(time);
		if(!Obj_IsDeleted(obj)&&!finish){
			let ang=ObjMove_GetAngle(obj);
			let spd=ObjMove_GetSpeed(obj);
			let x=ObjMove_GetX(obj);
			let y=ObjMove_GetY(obj);
			let bullet=ObjShot_GetImageID(obj);
			Obj_Delete(obj);
			obj = MyLaserz(x,y, spd,ang,accel,av,maxspd, segments,seg_rate, bullet, 0);
		}
	}
}

