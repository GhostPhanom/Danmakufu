#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["1und1 Nonspell3"]
#Text["1und1 Nonspell3" ]
#System["./System/Default_System.txt"]

#include"./Bulletsheet/Shotconstants.dnh" // ./../ bringt dich zum script Ordner
#include"./Generalfunction.dnh"

let objBoss;
let objBossSprite = 0;

let bossX=0;
let bossY=0;

let objScene = GetEnemyBossSceneObjectID();

@Initialize
{
	objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objBoss);
	//NotifyEvent(MyStageID, EV_USER, [0]);
	ObjMove_SetPosition(objBoss,GetCenterX,GetCenterY-100);
    //ObjMove_SetX(objBoss,3600);
	ObjEnemy_SetDamageRate(objBoss,500,100);
	SetPlayerLife(99);
	TDrawLoop;
	TFinalize;
	MainTask;
}

@MainLoop
{
    bossX = ObjMove_GetX(objBoss);
    bossY = ObjMove_GetY(objBoss);
    DeleteShotInCircle(TYPE_SHOT,TYPE_IMMEDIATE,0,0,2);
    ObjEnemy_SetIntersectionCircleToShot(objBoss,ObjMove_GetX(objBoss),ObjMove_GetY(objBoss),50);
    ObjEnemy_SetIntersectionCircleToPlayer(objBoss,ObjMove_GetX(objBoss),ObjMove_GetY(objBoss),40);
    yield;
}

@Event
{
    alternative(GetEventType())
    case(EV_REQUEST_LIFE)
    {
        SetScriptResult(4000);
    }
    case(EV_REQUEST_TIMER)
    {
        SetScriptResult(999);
    }
    case(EV_REQUEST_SPELL_SCORE)
    {
        SetScriptResult(1000000);
    }
	
	case(EV_USER) 
	{
		let value=GetEventArgument(0);
		if (value[0]==0){objBoss=value[1];} //when objBoss is asked
	}
}

task MainTask
{
	//ObjMove_SetDestAtFrame(objBoss,GetCenterX,GetCenterY,120);
	yield;
	Firetask;
	yield;
}

task Firetask
{
	let number = 8;
	let randx = 30;
	let randy = 20;
	while(!IsBossLifeZero)
	{
		FireFruits(number);
		wait(120);
		ObjMove_SetDestAtFrame(objBoss, GetCenterX + rand(randx * -1, randx), GetCenterY - 100 + rand(randy * -1, randy), 40);
		wait(50);
	}
}

task FireFruits(number)
{
	let angle = GetAngleToPlayer(objBoss);
	ascent(i in 0..number)
	{
		let obj=CreateShotA2(bossX, bossY, 4, angle + i * (360 / number), -0.08, 0, 327, 5);
		Fruit(obj, i);
	}
	
	task Fruit(obj, count)
	{
		ObjShot_SetSpellResist(obj, true);
		while(ObjMove_GetSpeed(obj) > 0)
		{
			if(IsBossLifeZero)
			{
				Obj_Delete(obj);
				return;
			}
			yield;
		}
		
		FadeDeleteDelay(obj, 0 , 40);
		let x = ObjMove_GetX(obj);
		let y = ObjMove_GetY(obj);
		let angle2 = rand(0, 360);
		if(count == 0){angle2 = GetAngleToPlayer(obj);}
		
		let number2 = 36;
		let number3 = 6;
		let dis = 20;
		let dis1 = 15;
		let delay = 5;
		let delay1 = 3;
		
		
		ascent(i in 0..number3)
		{
			ascent(j in 0..number2)
			{
				let angle3 = angle2 + (j * (360 / number2));
				CreateShotA2(x + (dis + dis1 * i) * cos(angle3), y + (dis + dis1 * i) * sin(angle3), 0, angle3, 0.035, 2, 1111, delay + delay1 * i);
			}
		}
		
	}
}



task TDrawLoop
{
	let scale=0.4;
    let imgBoss = GetCurrentScriptDirectory ~"./Sprite/Marcelldavis.png";
	Render(scale, imgBoss);

	task Render(scale,imgBoss)
	{
		let circle=0;
		let disy=4;
		objBossSprite=ObjEnemy_Create(OBJ_ENEMY);
		ObjEnemy_Regist(objBossSprite);
		ObjMove_SetPosition(objBossSprite,bossX,bossY);
		ObjPrim_SetTexture(objBossSprite,imgBoss);
		ObjSprite2D_SetSourceRect(objBossSprite,0,1,431,428);
		ObjSprite2D_SetDestCenter(objBossSprite);
		ObjRender_SetScaleXYZ(objBossSprite, scale, scale, scale);
		while(!Obj_IsDeleted(objBoss))
		{
			ObjMove_SetPosition(objBossSprite,bossX,bossY+disy*cos(circle));
			circle+=1.5;
			yield;
		}
		Obj_Delete(objBossSprite);
	}
	yield;
}

task TFinalize 
{
    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0){yield;}
    DeleteShotAll(TYPE_ALL, TYPE_IMMEDIATE);
    SetAutoDeleteObject(true);
    Obj_Delete(objBoss);
    CloseScript(GetOwnScriptID());
    
    if(ObjEnemyBossScene_GetInfo(objScene, INFO_PLAYER_SHOOTDOWN_COUNT) +ObjEnemyBossScene_GetInfo(objScene, INFO_PLAYER_SPELL_COUNT) == 0)
    {
        AddScore(ObjEnemyBossScene_GetInfo(objScene, INFO_SPELL_SCORE));
    }
    return;
}